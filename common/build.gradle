plugins {
    id 'fabric-loom'
}
base {
    archivesName = "${mod_name}-common-${minecraft_version}"
}
loom {
    if (file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runConfigs.configureEach {
        ideConfigGenerated = false
    }
}
if (System.getenv('BUILD_NUMBER') != null) {
    version += "." + System.getenv('BUILD_NUMBER')
}

test {
    useJUnitPlatform()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    // Need this for mixin deps
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"

    compileOnly 'org.spongepowered:mixin:0.8.5'
    compileOnly 'org.jetbrains:annotations:24.0.1'

    implementation "gg.moonflower:molang-compiler:${molang_compiler_version}"

    implementation "io.github.spair:imgui-java-binding:$imgui_version"
    implementation("io.github.spair:imgui-java-lwjgl3:$imgui_version") {
        exclude group: 'org.lwjgl'
        exclude group: 'org.lwjgl.lwjgl'
    }

    // VanillaGradle makes this impossible... https://github.com/SpongePowered/VanillaGradle/commit/ccc45765d9881747b2c922be7a13c453c32ce9ed
    implementation "org.lwjgl:lwjgl-opencl:$lwjgl_version"
    implementation "org.lwjgl:lwjgl-shaderc:$lwjgl_version"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}